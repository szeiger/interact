# Church numerals
cons Z
cons S(n)

# Erasure and Duplication
cons Erase                   deriving Erase
cons Dup(out1, out2) . in    deriving Erase
rule Dup(a, b) . Dup(c, d) = a . c, b . d

# Example: Manual dup and erase rules (instead of deriving)
rule Erase . Z = ()
rule Erase . S(x) = Erase . x
rule Dup(a, b) . Z  = a . Z, b . Z
rule Dup(a, b) . S(x) = x . Dup(sa, sb), a . S(sa), b . S(sb)

# Addition
cons Add(y, res) . x         deriving Erase, Dup
rule Add(y, r) . Z = y . r
rule Add(y, r) . S(x) = Add(S(y), r) . x

# Multiplication
cons Mult(y, res) . x        deriving Erase, Dup
rule Mult(y, r) . Z = r . Z, y . Erase
rule Mult(y, r) . S(x) = x . Mult(a, Add(b, r)), y . Dup (a, b)

# Example: Computations on church numerals
data y . S(S(S(S(S(Z))))),
     x . S(S(S(Z))),
     Add(y, example_3_plus_5) . x
data Mult(2'c, example_3_times_2) . 3'c

# Lists
cons Nil                     deriving Erase, Dup
cons Cons(head, tail) . l    deriving Erase, Dup

cons Length(res) . list      deriving Erase, Dup
rule Length(res) . Nil = res . Z
rule Length(res) . Cons(x, xs) = res . S(lxs), xs . Length(lxs), x . Erase

cons Map(fin, fout, res) . l deriving Erase, Dup
rule Map(fin, fout, res) . Nil = res . Nil, fin . Erase, fout.Erase
rule Map(fin, fout, res) . Cons(x, xs) = res . Cons(fouta, xsm), Map(finb, foutb, xsm) . xs, fin . Dup(fina, finb), fout . Dup(fouta, foutb), fina . x

# Example: List operations
data l0 . Cons(1'c, Cons(2'c, Cons(3'c, Nil))),
     l0 . Dup(l0a, l0b),
     Length(l0_length) . l0a,
     fin . Add(2'c, fout),
     Map(fin, fout, l0_mapped) . l0b

# Explicit lambdas
cons Lambda(in, out)         deriving Erase, Dup
cons Apply(in, out) . l      deriving Erase, Dup
rule Lambda(li, lo) . Apply(ai, ao) = li . ai, lo . ao

# Eample: List mapping with lambdas
cons Map2(f, res) . l        deriving Erase, Dup
rule Map2(f, res) . Nil = res . Nil, f . Erase
rule Map2(f, res) . Cons(x, xs) =
  res . Cons(fx, xsm),
  xs . Map2(fb, xsm),
  f . Dup(Apply(x, fx), fb)
data l0 . Cons(1'c, Cons(2'c, Cons(3'c, Nil))),
     f . Lambda(Add(2'c, fout), fout),
     Map2(f, l0_mapped_lambda) . l0
